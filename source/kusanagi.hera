## Currently adapting from Grammar.txt

Prog
  Init __ Imports:imps __ Declarations:decs ->
    return {
      type: "program",
      imports: imps,
      declarations: decs,
    }

Imports
  Imp ( ";" __ Imp )* __ ";"? ->
    return [$1].concat($2.map(([_1, _2, imp]) => imp))
  "" ->
    return []

Imp
  "import" __ PatNullary:pat __ "="? __ Text:source __ -> [source, pat]

Declarations
  Dec ( (";" / &EOS ) __ Dec )* __ ";"? __ ->
    return [$1].concat($2.map(([_1, _2, dec]) => dec))
  "" ->
    return []

Empty
  __ ->
    return ""

ObjSort
  "object"
  "actor"
  "module"

FuncSortOpt
  "shared" __ "query"?
  "query"
  Empty

SharedPatOpt
  ("shared" _)? ("query" _)? (PatPlain _)?

TypObj
  "{" __ DelimitedTypField*:decs "}" -> { type: "block", decs }
  $("{" Empty "}")
  # NOTE: Using "block" for code generation to handle semi-colon delimited object fields
  EOS NestedTypFields:decs -> { type: "block", decs }

DelimitedTypField
  TypField:field __ TypFieldDelimiter -> field

TypField
  $"var"?:prefix $(__ Id):id TypeSuffix:suffix -> { type: "typefield", prefix, id, suffix }
  # TODO: ast nodes
  Id Typing TypNullary TypeSuffix

TypFieldDelimiter
  # TypFields end with a semi-colon or an optional final semi-colon then the closing brace
  ";"  __
  ";"? __ &"}"

NestedTypFields
  PushIndent NestedTypField*:fields PopIndent ->
    if (!fields.length) return $skip
    return fields

NestedTypField
  Nested TypField:field &EOS -> field

TypVariant
  $(__ "{" __ "#" __ "}")
  # NOTE: Using block node for code generation since it wraps with braces and delimits with semi-colons
  __ "{" TypVariantField*:tags "}" -> { type: "block", decs: tags }
  # NOTE: Using block node for code generation since it wraps with braces and delimits with semi-colons
  EOS NestedTypTags:tags -> { type: "block", decs: tags }

TypVariantField
  __ TypTag:typTag TypFieldDelimiter -> typTag

NestedTypTags
  PushIndent NestedTypTag*:tags PopIndent ->
    if (!tags.length) return $skip
    return tags

NestedTypTag
  Nested TypTag:tag &EOS -> tag

TypNullary
  # NOTE: Overloading "parens" handler for code generation
  "(" TypItemField*:exps ")" -> { type: "parens", exps }
  $("(" Empty ")")
  # NOTE: Overloading "array" handler for code generation
  "[" $(__ "var")?:prefix __ Typ:typ __ "]" -> { type: "array", prefix, exps: [typ] }
  TypVariant
  TypObj
  $(Id ("." Id)*):id TypArgs?:typArgs -> { type: "type", id, typArgs }

TypItemField
  __ TypItem:item __ TypItemDelimiter -> item

TypItemDelimiter
  ","
  ","? __ &")"

TypUn
  TypNullary
  "?" TypUn

TypPre
  # NOTE: converted recursive "async" to optional
  "async"? __ ObjSort:type __ TypObj:body ->
    var exp = { type, body }
    if ($1) return { type: "async", exp }
    return exp

  ("async" _)? TypUn:exp ->
    if ($1) {
      return {type: "async", exp}
    } else {
      return exp
    }

TypNobin
  # TODO: AST nodes
  FuncSortOpt __ Typing? __ TypUn __ "->" __ TypNobin
  TypPre

Typ
  TypNobin:base TypRest*:rest ->
    if(rest.length) {
      return [base, ...rest]
    }
    return base
  # NOTE: Eliminated left recursion

TypRest
  __ "and":op __ Typ:type -> [op, type]
  __  "or":op __ Typ:type -> [op, type]

TypItem
  Id TypeSuffix
  Typ

TypArgs
  "<" TypArgField*:binds ">" -> { type: "typebind", binds }

TypArgField
  __ Typ:typ ( ("," __) / (","? __ &">") ) -> typ

TypTag
  $("#" Id __):id TypeSuffix?:suffix -> { type: "typetag", id, suffix }

TypBind
  Id __ "<:" __ Typ
  Id

Bool
  "true"
  "false"

Ascii
  [\x00-\x7f]

AsciiNoNL
  [\x00-\x09\x0b-\x7f]

Utf8Cont
  [\x80-\xbf]

Utf8Enc
  [\xc2-\xdf] Utf8Cont
  [\xe0] [\xa0-\xbf] Utf8Cont
  [\xed] [\x80-\x9f] Utf8Cont
  [\xe1-\xec\xee-\xef] Utf8Cont Utf8Cont
  [\xf0] [\x90-\xbf] Utf8Cont Utf8Cont
  [\xf4] [\x80-\x8f] Utf8Cont Utf8Cont
  [\xf1-\xf3] Utf8Cont Utf8Cont Utf8Cont

Utf8
  Ascii
  Utf8Enc

Utf8NoNL
  AsciiNoNL
  Utf8Enc

Escape
  [nrt\\'"]

# https://github.com/dfinity/motoko/blob/master/doc/modules/language-guide/pages/language-manual.adoc#characters
Character
  [^"\\\x00-\x1f\x7f-\xff]
  Utf8Enc
  "\\" Escape
  "\\" HexDigit HexDigit
  "\\u{" HexNumber "}"

# https://github.com/dfinity/motoko/blob/master/doc/modules/language-guide/pages/language-manual.adoc#text
Text
  $("\"" $(Character*) "\"")

Char
  $("'" Character "'")

ReservedWord
  /(actor|and|assert|await|break|case|catch|class|continue|debug|debug_show|do|else|flexible|false|for|func|if|ignore|in|import|not|null|object|or|label|let|loop|private|public|query|return|shared|stable|system|switch|true|try|type|var|while)\s/

# https://github.com/dfinity/motoko/blob/master/doc/modules/language-guide/pages/language-manual.adoc#identifiers
Id
  !ReservedWord $(/[a-zA-Z][a-zA-Z0-9_]*/):id -> id

Digit
  [0-9]

HexDigit
  [0-9a-fA-F]

Number
  # NOTE: Keeping numbers as a text string exactly as written
  $(Digit ( "_"? Digit )*)

HexNumber
  # NOTE: Keeping numbers as a text string exactly as written
  $(HexDigit ( "_"? HexDigit )*)

# https://github.com/dfinity/motoko/blob/master/doc/modules/language-guide/pages/language-manual.adoc#integers
Nat
  "0x" HexNumber
  Number

# https://github.com/dfinity/motoko/blob/master/doc/modules/language-guide/pages/language-manual.adoc#floats
Float
  "0x" HexNumber "." HexNumber?
  "0x" HexNumber ( "." HexNumber? )? [pP] [-+]? Number
  Number "." Number?
  Number ( "." Number? )? [eE] [-+]? Number

# https://github.com/dfinity/motoko/blob/master/doc/modules/language-guide/pages/language-manual.adoc#literals
Lit
  "null"
  Bool
  $(Nat)
  $(Float)
  Char
  Text

# https://github.com/dfinity/motoko/blob/master/doc/modules/language-guide/pages/language-manual.adoc#unary-operators
Unop
  "+"
  "-"
  "^"

# https://github.com/dfinity/motoko/blob/master/doc/modules/language-guide/pages/language-manual.adoc#numeric-binary-operators
# https://github.com/dfinity/motoko/blob/master/doc/modules/language-guide/pages/language-manual.adoc#bitwise-and-wrapping-binary-operators
Binop
  "**"
  "+%"
  "-%"
  "*%"
  "**%"
  "<<>"
  "<>>"
  "<<"
  ">>" # NOTE: removed leading whitespace
  # https://github.com/dfinity/motoko/blob/master/doc/modules/language-guide/pages/language-manual.adoc#text-operators
  "#"
  "+"
  "-"
  "*"
  "/"
  "%"
  "&"
  "|"
  "^"

# https://github.com/dfinity/motoko/blob/master/doc/modules/language-guide/pages/language-manual.adoc#relational-operators
# NOTE: removed surrounding whitespace from angle brackets since PEG doesn't have a separate tokenization phase and has more
# context for the operator. May need to add it back later to be strictly match the spec. Can likely use a RegExp lookahead
# assertion for the whitespace matching in ExpBinRest if necessary.
Relop
  "=="
  "!="
  "<="
  ">="
  "<" # NOTE: removed surrounding whitespace
  ">" # NOTE: removed surrounding whitespace

# https://github.com/dfinity/motoko/blob/master/doc/modules/language-guide/pages/language-manual.adoc#assignment-operators
Unassign
  "+="
  "-="
  "^="

# https://github.com/dfinity/motoko/blob/master/doc/modules/language-guide/pages/language-manual.adoc#assignment-operators
Binassign
  "+="
  "-="
  "*="
  "/="
  "%="
  "**-"
  "+%="
  "-%="
  "*%="
  "**%="
  "&="
  "|="
  "^="
  "<<="
  ">>="
  "<<>="
  "<>>="
  "@="
  "#=" # NOTE: Added
  ":=" # NOTE: Moved here from ExpNondec

ExpObj
  # Object Pattern https://github.com/dfinity/motoko/blob/master/doc/modules/language-guide/pages/language-manual.adoc#object-pattern
  # This different from PatNullary because exp fields can optionally start with "var"
  $( __ "{" __ ExpObjField* "}")
  # NOTE: Added indentation based object syntax
  __ "{" EOS NestedObjectFields:fields __ "}" -> { type: "block", decs: fields }

ExpObjField
  # NOTE: Extracted field rule for easier iteration
  ExpField:field __ ( ";" / (__ &"}") ) __ -> field

NestedObjectFields
  PushIndent NestedObjectField*:fields PopIndent -> fields

NestedObjectField
  Nested ExpField:field &EOS -> field

ExpPlain
  _? Lit:lit -> lit
  # NOTE: Extracted expression part for easier iteration
  _? "(" __ ExpPlainPart*:exps ")" -> { type: "parens", exps }
  # NOTE: Added Parentheses-less tuple
  _ Exp:first CommaExp*:exps ->
    return { type: "parens", exps: [first, ...exps] }

ExpPlainPart
  Exp:exp __ ( "," / (__ &")") ) __ -> exp

CommaExp
  "," __ Exp:exp -> exp

ExpNullary
  ExpObj
  ExpPlain
  Id

ExpPost
  ExpNullary:exp ExpPostRest*:rest ->
    if (rest.length) {
      return {
        // TODO better naming
        type: "exppost",
        base: exp,
        rest: rest,
      }
    } else {
      return exp
    }
  # Array
  # https://github.com/dfinity/motoko/blob/master/doc/modules/language-guide/pages/language-manual.adoc#arrays
  # NOTE: Added indentation based array syntax
  "[" $((_? "var")?):prefix EOS NestedArrayParts:exps __ "]" -> { type: "array", prefix, exps }
  "[" __ $(( "var" __ )?):prefix ArrayPart*:exps "]" -> { type: "array", prefix, exps }
  # NOTE: Eliminated left recursion

ArrayPart
  ExpNonvar:exp ( "," / (__ &"]") ) __ -> exp

NestedArrayParts
  PushIndent NestedArrayPart*:exps PopIndent -> exps

NestedArrayPart
  Nested ExpNonvar:exp &EOS -> exp

ExpPostRest
  # Array Indexing
  # https://github.com/dfinity/motoko/blob/master/doc/modules/language-guide/pages/language-manual.adoc#array-indexing
  "[" Exp:exp "]" ->
    return {
      type: "index",
      exp,
    }
  # Tuple Projection
  # https://github.com/dfinity/motoko/blob/master/doc/modules/language-guide/pages/language-manual.adoc#tuples
  "." Nat:nat ->
    return {
      type: "projection",
      id: nat
    }
  # Object Projection (member access)
  # https://github.com/dfinity/motoko/blob/master/doc/modules/language-guide/pages/language-manual.adoc#object-projection-member-access
  "." Id:id ->
    return {
      type: "projection",
      id,
    }
  # Function application
  $((TypArgs __ )?):typArgs ExpNullary:fnArgs ->
    return {
      type: "application",
      typArgs,
      fnArgs
    }
  "!" # Null break

ExpUn
  ExpPost
  # Variant Pattern
  # NOTE: Combined and added `?` to prevent shadowing
  # https://github.com/dfinity/motoko/blob/master/doc/modules/language-guide/pages/language-manual.adoc#variant-pattern
  "#" Id __ ExpNullary?
  "?" ExpUn
  Unop ExpUn
  Unassign ExpUn
  "actor" ExpPlain
  "not" ExpUn
  "debug_show" ExpUn
  "to_candid" "(" ( Exp ( "," Exp )* )? ")"
  "from_candid" ExpUn

ExpBin
  # NOTE: Eliminated left recursion
  ExpUn:exp ExpBinRest*:rest ->
    if (rest.length) {
      return {
        type: "expbin",
        exps: [exp].concat(...rest),
      }
    } else {
      return exp
    }

ExpBinRest
  __ Binop __ ExpBin    -> [$2, $4]
  __ Relop __ ExpBin    -> [$2, $4]
  __ "and" __ ExpBin    -> [$2, $4]
  __ "or"  __ ExpBin    -> [$2, $4]
  __ ":"   __ TypNobin  -> [$2, $4]

ExpNondec
  "return" __ Exp?:exp ->
    return {type: "return", exp}
  "async"    ExpNest:exp -> { type: "async",  exp }
  "await"    ExpNest:exp -> { type: "await",  exp }
  "assert"   ExpNest:exp -> { type: "assert", exp }
  "label"    __ Id:id __ $(TypeSuffix?):typeSuffix ExpNest:exp -> { type: "label", id, typeSuffix, exp }
  "break"    __ Id:id __ ExpNullary?:exp -> { type: "break", id, exp }
  "continue" __ Id:id ->       { type: "continue", id }
  "debug"    ExpNest:exp -> { type: "debug", exp }
  "if"       ExpNullary:condition ExpNest:exp ElseBlock?:elseBlock -> { type: "if", condition, exp, elseBlock }
  "try"      ExpNest:exp __ Catch:c -> { type: "try", exp, catch: c }
  "throw"    ExpNest:exp -> { type: "throw", exp }
  "switch"   ExpNullary:condition __ "{" __ Case*:cases "}" -> { type: "switch", condition, cases }
  # NOTE: Added brace-less switch
  "switch"   ExpNullary:condition EOS NestedCases:cases -> { type: "switch", condition, cases }
  "while"    ExpNullary:condition ExpNest:exp -> { type: "while", condition, exp }
  "loop"     ExpNest:exp WhileBlock?:whileBlock -> { type: "loop", exp, whileBlock }
  "for"      __ "(" __ Pat:pat __ "in" __ Exp:source __ ")" ExpNest:exp -> { type: "for", pat, source, exp }
  # NOTE: Added paren-less `for`
  "for"      _ Pat:pat __ "in" __ Exp:source ExpNest:exp -> { type: "for", pat, source, exp }
  "ignore"   ExpNest:exp -> { type: "ignore", exp }
  "do"        Block:block -> { type: "do", block }
  "do" __ "?" Block ->       { type: "do", block, option: true }
  # NOTE: Moved ":=" into Binassign
  ExpBin:base __ Binassign:op __ Exp:exp -> { type: "binassign", base, op, exp}
  ExpBin

ElseBlock
  __ "else" ExpNest:exp -> exp

WhileBlock
  __ "while" ExpNest:exp -> exp

ExpNonvar
  ExpNondec
  DecNonvar

Exp
  ExpNonvar
  DecVar

ExpNest
  Block
  __ Exp:exp -> exp

Block
  __ "{" __ BlockDeclaration*:decs "}" -> { type: "block", decs }
  EOS NestedBlockDeclarations:decs ->
    // Skip if there were no indented declarations found
    if (!decs.length) return $skip

    return { type: "block", decs, nested: true }

NestedBlockDeclarations
  PushIndent NestedBlockDeclaration*:decs PopIndent -> decs

NestedBlockDeclaration
  Nested Dec:dec &EOS -> dec

# NOTE: Extracted block declaration with optional trailing semi-colon when object closes to make field iteration easier in Block
BlockDeclaration
  Dec:dec __ ( ";" / (__ &"}") ) __ -> dec

Case
  "case" PatNullary:pat ExpNest:exp  __ ( ";" / &"}" ) __ ->
    return {
      type: "case",
      pat,
      exp,
    }

NestedCases
  PushIndent NestedCase*:cases PopIndent -> cases

NestedCase
  Nested "case" PatNullary:pat ExpNest:exp &EOS ->
    return {
      type: "case",
      pat,
      exp,
    }

Catch
  "catch" PatNullary:pat ExpNest:exp ->
    return {
      type: "catch",
      pat,
      exp,
    }

ExpField
  "var"? __ Id TypeSuffix? (__ "=" __ Exp)?

DecField
  $(( Vis __ )? ( Stab __ )?):prefix Dec:dec ->
    return { type: "dec", prefix, dec }

Vis
  "private"
  "public"
  "system"

Stab
  "flexible"
  "stable"

PatPlain
  __ "(" __ PatPlainPart*:parts ")" ->
    return {
      type: "parens",
      exps: parts,
    }
  # NOTE: Added Parentheses-less tuple
  _ PatBin:first CommaPat*:pats ->
    return { type: "parens", exps: [first, ...pats] }
  "_"
  Id
  Lit

# NOTE: Extracted PatPlainPart with optional trailing comma before close parenthesis
PatPlainPart
  PatBin:pat __ ( "," / (__ &")") ) __ -> pat

CommaPat
  "," __ PatBin:pat -> pat

PatNullary
  PatPlain
  # Object Pattern https://github.com/dfinity/motoko/blob/master/doc/modules/language-guide/pages/language-manual.adoc#object-pattern
  # TODO: Add indentation based object syntax
  $("{" ( PatField (";" PatField )* )? "}") # Return string as written for now

PatUn
  PatNullary
  "#" Id PatNullary? # NOTE: Merged in "#" Id with optional nullary to prevent shadowing
  "?" __ PatUn
  Unop Lit

PatBin
  PatUn:first PatBinRest*:rest ->
    if (!rest.length) {
      return first
    } else {
      return [first].concat(...rest)
    }
  # NOTE: Eliminated left recursion

PatBinRest
  __ "or" __ PatBin -> [$2, $4]
  __ ":"  __ Typ    -> [$2, $4]

Pat
  PatBin

PatField
  Id TypeSuffix? __ "=" __  Pat
  Id TypeSuffix?

DecVar
  "var" __ Id:id __ $(TypeSuffix?):typeSuffix __ "=" __ Exp:exp ->
    return { type: "var", id, exp, typeSuffix}

DecNonvar
  "let"  __ Pat:pat  __ "=" __ Exp:exp -> { type: "let", pat, exp}
  "type" $(__ Id):id __ Typing?:typing __ "=" _? Typ:exp -> { type: "typedec", id, typing, exp }

  ObjSort:type $(__ Id)?:id (__ "=")? ObjBody:body ->
    return { id, type, body }

  $(SharedPatOpt):shared "func" _ $(Id?):id __ Typing?:typing __ PatPlain:pat $(TypeSuffix?):typeSuffix FuncBody:body ->
    return { type: "func", shared, id, typing, pat, typeSuffix, body}
  $(SharedPatOpt):shared $(ObjSort?):sort __ "class" __ Id?:id __ Typing?:typing __ PatPlain:pat $(TypeSuffix?):typeSuffix ClassBody:body ->
    return { type: "class", sort, shared, id, typing, pat, typeSuffix, body}

TypeSuffix
  __ ":" __ Typ:typ -> typ

Typing
  "<" TypingField*:binds ">" -> { type: "typebind", binds }

TypingField
  TypBind:typBind __ ( ("," __) / &">" ) -> typBind

Dec
  DecVar
  DecNonvar
  ExpNondec

FuncBody
  Block
  __ "=" __ Exp:exp ->
    return {
      type: "=",
      exp
    }

ObjBody
  __ "{" __ ObjBodyField*:decs "}" __ ->
    return {
      type: "block",
      decs: decs,
    }
  EOS NestedObjBodyFields:decs ->
    return {
      type: "block",
      decs: decs,
    }

# NOTE: Extracted dec-field with optional trailing semi-colon when object closes to make field iteration easier in ObjBody
ObjBodyField
  DecField:field __ ( ";" / (__ &"}") ) __ -> field

NestedObjBodyFields
  PushIndent NestedObjBodyField*:fields PopIndent -> fields

NestedObjBodyField
  Nested DecField:field &EOS -> field

ClassBody
  __ "=" __ Id?:id __ ObjBody:exp ->
    return {
      type: "=",
      id,
      exp,
    }
  ObjBody

# https://github.com/dfinity/motoko/blob/master/doc/modules/language-guide/pages/language-manual.adoc#comments
Comment
  $MultilineComment  -> { type: "comment", comment: $1 }
  $SingleLineComment -> { type: "comment", comment: $1 }

# https://github.com/dfinity/motoko/blob/master/doc/modules/language-guide/pages/language-manual.adoc#comments
MultilineComment
  # Note: Nested Comments
  "/*" ( (!("/*" / "*/") . ) / MultilineComment )* "*/"

# https://github.com/dfinity/motoko/blob/master/doc/modules/language-guide/pages/language-manual.adoc#comments
SingleLineComment
  /\/\/[^\r\n]*/

# Any or no whitespace including newlines and comments
__
  ([\s]+ / Comment)*

# Non-newline whitespace
_
  /[ \t]+/

# End of statemnt
EOS
  ((_? Comment?)* EOL)+ _ EOF
  ((_? Comment?)* EOL)+
  EOF

# End of line
EOL
  "\r\n"
  "\n"
  "\r"

# End of file
EOF
  !/[\s\S]/

# Indentation management

Indent
  ( "  " / "\t" )* ->
    return $1.length

Nested
  EOS? Indent:indent ->
    const currentIndent = global.indentLevels[global.indentLevels.length-1]
    if (global.verbose) {
      console.log("global indent", global.currentIndent)
      console.log("Indented", indent, currentIndent)
    }
    if (indent !== currentIndent) {
      if (global.verbose) {
        console.log("skipped nested")
      }
      return $skip
    }
    return indent

Init
  "" ->
    global.currentIndent = 0
    global.indentLevels = [0]
    global.verbose = false

PushIndent
  "" ->
    global.currentIndent++
    if (global.verbose) {
      console.log("pushing indent", global.currentIndent)
    }
    global.indentLevels.push(global.currentIndent)

PopIndent
  "" ->
    if (global.verbose) {
      console.log("popping indent", global.indentLevels[global.indentLevels.length-1], "->", global.indentLevels[global.indentLevels.length-2])
    }
    global.indentLevels.pop()
    global.currentIndent = global.indentLevels[global.indentLevels.length-1]
